import java.lang.reflect.Field

plugins {
    id 'java'
    id 'maven-publish'
    id("io.github.sgtsilvio.gradle.maven-central-publishing") version "0.4.0"
}

def ref = System.getenv("GITHUB_REF")
version = (ref != null && ref.startsWith("refs/tags/")) ? ref.tokenize('/').last().replaceFirst(/^v/, '') : p_version

String mavenUrl = System.getenv("MAVEN_URL")

String envRepo = System.getenv("MAVEN_REPO");
String mavenRepo = envRepo != null ? envRepo : "releases";

Project main = project;

subprojects {
    repositories {
        mavenCentral()
        mavenLocal()

        maven { url 'https://s01.oss.sonatype.org' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://jitpack.io' }
        maven { url 'https://teavm.org/maven/repository/' }
    }

    afterEvaluate {
        println "Subproject: ${project.name}"
        if (project.name == "backends") {
            main.dependencies {
                testImplementation sourceSets.getByName("java2d").output
                testImplementation sourceSets.getByName("gdxCore").output
            }
        }
    }

}

repositories {
    mavenCentral()
    mavenLocal()
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    testImplementation "com.badlogicgames.gdx:gdx:$gdx_version"
    testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdx_version"
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdx_version:natives-desktop"
}

dependencies {
    implementation "dev.puzzleshq:puzzle-annotations:$puzzle_annotations"

//    implementation "dev.puzzleshq:puzzle-loader-core:1.0.0:common"
}

publishing {
    repositories {
        maven {
            name = "PuzzleHQsMaven"
            url = mavenUrl+ "/" +mavenRepo
            credentials {
                username = System.getenv("MAVEN_NAME")
                password = System.getenv("MAVEN_SECRET")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = p_group
            artifactId = p_id
            version = this.version

            from components.java

            pom {
                name = p_name
                description = p_desc
                url = p_git

                licenses {
                    license {
                        name = "GNU Lesser General Public License"
                        url = "https://www.gnu.org/licenses/lgpl-2.1.html"
                        distribution = "LGPL"
                        comments = "This project includes LGPL-licensed components."
                    }
                }

                developers {
                    developer {
                        id = 'mrzombii'
                        name = 'Mr Zombii'
                        email = 'thezombiigames@gmail.com'
                    }
                }
                scm {
                    connection = "${p_git}.git"
                    developerConnection = "${p_git}.git"
                    url = "${p_git}.git"
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign(publishing.publications[0])
}

wrapper {
    gradleVersion = "8.5"
    distributionType = Wrapper.DistributionType.ALL
}

java.toolchain.languageVersion.set(JavaLanguageVersion.of(8))
